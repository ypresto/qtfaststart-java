buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

repositories {
    jcenter()
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'net.ypresto.qtfaststartjava'
            artifactId 'qtfaststart'
            version '0.1.0'

            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : null;
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : null;
    publications = ['maven']
    publish = false
    pkg {
        repo = 'maven'
        name = 'qtfaststart-java'
        desc = 'A qt-faststart translated into Java, MIT licensed.'
        websiteUrl = 'https://github.com/ypresto/qtfaststart-java'
        issueTrackerUrl = 'https://github.com/ypresto/qtfaststart-java/issues'
        vcsUrl = 'https://github.com/ypresto/qtfaststart-java.git'
        licenses = ['MIT']
        labels = ['mp4']
        publicDownloadNumbers = false
        version {
            name = '0.1.0'
            vcsTag = '0.1.0'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.11'
}

task sourceJar(type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
